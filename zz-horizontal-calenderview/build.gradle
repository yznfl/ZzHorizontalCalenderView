apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.0"

android {
    resourcePrefix "zz_" //随意填写

    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    compile 'com.android.support:recyclerview-v7:24.2.1'
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

group = "me.zhouzhuo.zzhorizontalcalenderview"            // Maven Group ID for the artifact，一般填你唯一的包名
install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId 'me.zhouzhuo.zzhorizontalcalenderview' //自己定义的组名
            artifactId 'zz-horizontal-calenderview'

            name 'zz-horizontal-calenderview'
            description 'A horizontal and scrollable CalenderView.'
            url 'https://github.com/zhouzhuo810/ZzHorizontalCalenderView'
            inceptionYear '2016'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/zhouzhuo810/ZzHorizontalCalenderView.git'
                url 'https://github.com/zhouzhuo810/ZzHorizontalCalenderView'

            }
            developers {
                developer {
                    name 'zhouzhuo'
                    email 'admin@zhouzhuo.me'
                }
            }
        }
    }
}

// Bintray

//获取local.propertes的信息
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publish = true
    configurations = ['archives']
    pkg {
        //填入 bintray 上对应的 package 信息
        repo = 'maven'
        name = 'zz-horizontal-calenderview'
        vcsUrl = 'https://github.com/zhouzhuo810/ZzHorizontalCalenderView.git'
        websiteUrl = 'https://github.com/zhouzhuo810/ZzHorizontalCalenderView'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/zhouzhuo810/ZzHorizontalCalenderView/issues'
        publicDownloadNumbers = true
        version {
            name = '1.0.0'
            desc = 'zz-horizontal-calenderview release'
            vcsTag = '1.0.0'
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task findConventions << {
    println project.getConvention()
}
